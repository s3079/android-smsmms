plugins {
    id 'com.android.library'
    id 'maven-publish'
}

dependencies {
    implementation 'com.klinkerapps:logger:1.0.3'
    implementation 'com.squareup.okhttp:okhttp:2.5.0'
    implementation 'com.squareup.okhttp:okhttp-urlconnection:2.5.0'
}

android {
    compileSdkVersion 25

    defaultConfig {
        minSdkVersion 21
        //noinspection ExpiredTargetSdkVersion -- SDK 31 has hidden several methods of the ConnectivityManager class, such as startUsingNetworkFeature
        targetSdkVersion 25
    }

    lintOptions {
        abortOnError false
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }

    useLibrary 'org.apache.http.legacy'
    namespace 'com.klinker.android.send_message'
}

afterEvaluate {
    publishing {
        publications {
            maven(MavenPublication) {
                from components.release
                groupId = 'org.kde'
                artifactId = 'android-smsmms'
                version = '5.2.7'

                pom {
                    name = 'Android smsmms'
                    description = 'A library to simplify sending SMS and MMS on Android, with patches for KDE Connect.'
                 }
            }
        }

        repositories {
            maven {
                name = "KDE Invent Maven"
                // Push to the repository for the kdeconnect-android project
                // Note that this repository is not allowed by f-droid, so publishing this way is
                // not needed to us. kdeconnect-android pulls from jitpack instead:
                // https://jitpack.io/#org.kde.invent.sredman/android-smsmms
                url = "https://invent.kde.org/api/v4/projects/72/packages/maven"
                credentials(HttpHeaderCredentials) {
                    name = "Private-Token"
                    value = "$System.env.TOKEN"
                }
                authentication {
                    header(HttpHeaderAuthentication)
                }
            }
        }
    }
}
